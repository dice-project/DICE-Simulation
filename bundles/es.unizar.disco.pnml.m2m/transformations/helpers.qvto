import es.unizar.disco.pnml.utils.PnmlDiceUtils;

modeltype UML uses 'http://www.eclipse.org/uml2/5.0.0/UML';
modeltype PNML uses 'http:///ptnet.ecore';
modeltype TRACE uses 'http://es.unizar.disco/simulation/traces/1.0';
modeltype TYPES uses 'http://es.unizar.disco/simulation/datatypes/1.0';
modeltype CONST uses 'http://es.unizar.disco/pnconstants/1.0';
modeltype ECORE uses 'http://www.eclipse.org/emf/2002/Ecore';

library helpers;

/*******************************************************************************
  Types helpers
*******************************************************************************/

helper OclAny::eObject() : ECORE::EObject {
	return self.oclAsType(ECORE::EObject);
}

/*******************************************************************************
  Getters for stereotypes
*******************************************************************************/

/* GaElements */

helper UML::Element::getGaWorkloadEvent() : UML::Stereotype {
	if (self.isStereotypeApplied(self.getAppliedStereotype("MARTE::MARTE_AnalysisModel::GQAM::GaWorkloadEvent"))) { 
		return self.getAppliedStereotype("MARTE::MARTE_AnalysisModel::GQAM::GaWorkloadEvent");
	};
	return null;	
}

helper UML::Element::getGaStep() : UML::Stereotype {
	if (self.isStereotypeApplied(self.getAppliedStereotype("MARTE::MARTE_AnalysisModel::GQAM::GaStep"))) { 
		return self.getAppliedStereotype("MARTE::MARTE_AnalysisModel::GQAM::GaStep");
	};
	return null;	
}

/*helper UML::Element::getDaStep() : UML::Stereotype {
	if (self.isStereotypeApplied(self.getApplicableStereotype("DAM::DAM_UML_Extensions::System::Core::DaStep"))) { 
		return self.getAppliedStereotype("DAM::DAM_UML_Extensions::System::Core::DaStep");
	};
	return null;	
}*/

helper UML::Element::getDaComponent() : UML::Stereotype {
	if (self.isStereotypeApplied(self.getAppliedStereotype("DAM::DAM_UML_Extensions::System::Core::DaComponent"))) { 
		return self.getAppliedStereotype("DAM::DAM_UML_Extensions::System::Core::DaComponent");
	};
	return null;	
}

helper UML::Element::getDaStep() : UML::Stereotype {
	if (self.isStereotypeApplied(self.getAppliedStereotype("DAM::DAM_UML_Extensions::System::Core::DaStep"))) { 
		return self.getAppliedStereotype("DAM::DAM_UML_Extensions::System::Core::DaStep");
	};
	return null;	
}

helper UML::Element::getGaCommStep() : UML::Stereotype {
	if (self.isStereotypeApplied(self.getAppliedStereotype("MARTE::MARTE_AnalysisModel::GQAM::GaCommStep"))) { 
		return self.getAppliedStereotype("MARTE::MARTE_AnalysisModel::GQAM::GaCommStep");
	};
	return null;	
}

helper UML::Element::getPaLogicalResource() : UML::Stereotype {
	if (self.isStereotypeApplied(self.getAppliedStereotype("MARTE::MARTE_AnalysisModel::PAM::PaLogicalResource"))) { 
		return self.getAppliedStereotype("MARTE::MARTE_AnalysisModel::PAM::PaLogicalResource");
	};
	return null;	
}

/* Storm Elements */

helper UML::Element::getStormSpout() : UML::Stereotype {
	if (self.isStereotypeApplied(self.getAppliedStereotype("DICE::DICE_UML_Extensions::DTSM::Storm::StormSpout"))) { 
		return self.getAppliedStereotype("DICE::DICE_UML_Extensions::DTSM::Storm::StormSpout");
	};
	return null;	
}

helper UML::Element::getStormBolt() : UML::Stereotype {
	if (self.isStereotypeApplied(self.getAppliedStereotype("DICE::DICE_UML_Extensions::DTSM::Storm::StormBolt"))) { 
		return self.getAppliedStereotype("DICE::DICE_UML_Extensions::DTSM::Storm::StormBolt");
	};
	return null;	
}

helper UML::Element::getStormStreamStep() : UML::Stereotype {
	if (self.isStereotypeApplied(self.getAppliedStereotype("DICE::DICE_UML_Extensions::DTSM::Storm::StormStreamStep"))) { 
		return self.getAppliedStereotype("DICE::DICE_UML_Extensions::DTSM::Storm::StormStreamStep");
	};
	return null;	
}

helper UML::Element::getPaRunTInstance() : UML::Stereotype {
	if (self.isStereotypeApplied(self.getAppliedStereotype("MARTE::MARTE_AnalysisModel::PAM::PaRunTInstance"))) { 
		return self.getAppliedStereotype("MARTE::MARTE_AnalysisModel::PAM::PaRunTInstance");
	};
	return null;	
}

helper UML::Element::getGaExecHost() : UML::Stereotype {
	if (self.isStereotypeApplied(self.getAppliedStereotype("MARTE::MARTE_AnalysisModel::GQAM::GaExecHost"))) { 
		return self.getAppliedStereotype("MARTE::MARTE_AnalysisModel::GQAM::GaExecHost");
	};
	return null;	
}

helper UML::Node::getStormZookeeper() : UML::Stereotype {
	if (self.isStereotypeApplied(self.getAppliedStereotype("DICE::DICE_UML_Extensions::DTSM::Storm::StormZookeeper"))){
		return self.getAppliedStereotype("DICE::DICE_UML_Extensions::DTSM::Storm::StormZookeeper");
	};
	return null;
}

/* Hadoop Elements */

helper UML::Element::getHadoopOperation() : UML::Stereotype {
	if (self.isStereotypeApplied(self.getApplicableStereotype("DICE::DICE_UML_Extensions::DTSM::Hadoop::HadoopOperation"))) { 
		return self.getAppliedStereotype("DICE::DICE_UML_Extensions::DTSM::Hadoop::HadoopOperation");
	};
	return null;	
}

helper UML::Element::getHadoopMap() : UML::Stereotype {
	if (self.isStereotypeApplied(self.getApplicableStereotype("DICE::DICE_UML_Extensions::DTSM::Hadoop::HadoopMap"))) { 
		return self.getAppliedStereotype("DICE::DICE_UML_Extensions::DTSM::Hadoop::HadoopMap");
	};
	return null;	
}

helper UML::Element::getHadoopReduce() : UML::Stereotype {
	if (self.isStereotypeApplied(self.getApplicableStereotype("DICE::DICE_UML_Extensions::DTSM::Hadoop::HadoopReduce"))) { 
		return self.getAppliedStereotype("DICE::DICE_UML_Extensions::DTSM::Hadoop::HadoopReduce");
	};
	return null;	
}

helper UML::Element::getHadoopWorkloadEvent() : UML::Stereotype {
	if (self.isStereotypeApplied(self.getApplicableStereotype("DICE::DICE_UML_Extensions::DTSM::Hadoop::HadoopWorkloadEvent"))) { 
		return self.getAppliedStereotype("DICE::DICE_UML_Extensions::DTSM::Hadoop::HadoopWorkloadEvent");
	};
	return null;	
}

helper UML::Element::getHadoopComputationNode() : UML::Stereotype {
	if (self.isStereotypeApplied(self.getAppliedStereotype("DICE::DICE_UML_Extensions::DTSM::Hadoop::HadoopComputationNode"))) { 
		return self.getAppliedStereotype("DICE::DICE_UML_Extensions::DTSM::Hadoop::HadoopComputationNode");
	};
	return null;	
}

helper UML::Element::getHadoopScenario() : UML::Stereotype {
	if (self.isStereotypeApplied(self.getApplicableStereotype("DICE::DICE_UML_Extensions::DTSM::Hadoop::HadoopScenario"))) { 
		return self.getAppliedStereotype("DICE::DICE_UML_Extensions::DTSM::Hadoop::HadoopScenario");
	};
	return null;	
}

/* Spark Elements */

helper UML::Element::getSparkOperation() : UML::Stereotype {
	if (self.isStereotypeApplied(self.getApplicableStereotype("DICE::DICE_UML_Extensions::DTSM::Spark::SparkOperation"))) { 
		return self.getAppliedStereotype("DICE::DICE_UML_Extensions::DTSM::Spark::SparkOperation");
	};
	return null;	
}

helper UML::Element::getSparkMap() : UML::Stereotype {
	/*if (self.isStereotypeApplied(self.getApplicableStereotype("DICE::DICE_UML_Extensions::DTSM::Spark::SparkMap"))) { 
		return self.getAppliedStereotype("DICE::DICE_UML_Extensions::DTSM::Spark::SparkMap");
	};*/
	if (self.isStereotypeApplied(self.getApplicableStereotype("DICE::DICE_UML_Extensions::DTSM::Spark::SparkTransformation"))) { 
		return self.getAppliedStereotype("DICE::DICE_UML_Extensions::DTSM::Spark::SparkTransformation");
	};
	return null;	
}

helper UML::Element::getSparkReduce() : UML::Stereotype {
	/*if (self.isStereotypeApplied(self.getApplicableStereotype("DICE::DICE_UML_Extensions::DTSM::Spark::SparkReduce"))) { 
		return self.getAppliedStereotype("DICE::DICE_UML_Extensions::DTSM::Spark::SparkReduce");
	};*/
	if (self.isStereotypeApplied(self.getApplicableStereotype("DICE::DICE_UML_Extensions::DTSM::Spark::SparkAction"))) { 
		return self.getAppliedStereotype("DICE::DICE_UML_Extensions::DTSM::Spark::SparkAction");
	};
	return null;	
}

helper UML::Element::getSparkForEach() : UML::Stereotype {
	if (self.isStereotypeApplied(self.getApplicableStereotype("DICE::DICE_UML_Extensions::DTSM::Spark::SparkForEach"))) { 
		return self.getAppliedStereotype("DICE::DICE_UML_Extensions::DTSM::Spark::SparkForEach");
	};
	return null;	
}

helper UML::Element::getSparkWorkloadEvent() : UML::Stereotype {
	if (self.isStereotypeApplied(self.getApplicableStereotype("DICE::DICE_UML_Extensions::DTSM::Spark::SparkWorkloadEvent"))) { 
		return self.getAppliedStereotype("DICE::DICE_UML_Extensions::DTSM::Spark::SparkWorkloadEvent");
	};
	return null;	
}

helper UML::Element::getSparkNode() : UML::Stereotype {
	if (self.isStereotypeApplied(self.getAppliedStereotype("DICE::DICE_UML_Extensions::DTSM::Spark::SparkNode"))) { 
		return self.getAppliedStereotype("DICE::DICE_UML_Extensions::DTSM::Spark::SparkNode");
	};
	return null;	
}

helper UML::Element::getSparkScenario() : UML::Stereotype {
	if (self.isStereotypeApplied(self.getApplicableStereotype("DICE::DICE_UML_Extensions::DTSM::Spark::SparkScenario"))) { 
		return self.getAppliedStereotype("DICE::DICE_UML_Extensions::DTSM::Spark::SparkScenario");
	};
	return null;	
}


/*******************************************************************************
  ToolInfo utilities
*******************************************************************************/
/**
	Creates the ToolInfo that identifies an inhibitor arcs,
	i.e., CONST::ArcKind::Inhibitor 
*/
helper inhibitorArcToolInfo() : PNML::ToolInfo {
	return object PNML::ToolInfo { 
		tool := CONST::ToolInfoConstants::toolName.toString();
		version := CONST::ToolInfoConstants::toolVersion.toString();
		toolInfoGrammarURI := CONST::ArcKind::Inhibitor.toString().createURI();
		formattedXMLBuffer := ("<value grammar=\"" + CONST::ArcKind::Inhibitor.toString() + "\"/>").createLongString();
	};	
}

/**
	Creates the ToolInfo that identifies an inhibitor arcs,
	i.e., CONST::ArcKind::Inhibitor 
*/
helper normalArcToolInfo() : PNML::ToolInfo {
	return object PNML::ToolInfo { 
		tool := CONST::ToolInfoConstants::toolName.toString();
		version := CONST::ToolInfoConstants::toolVersion.toString();
		toolInfoGrammarURI := CONST::ArcKind::Inhibitor.toString().createURI();
		formattedXMLBuffer := ("<value grammar=\"" + CONST::ArcKind::Normal.toString() + "\"/>").createLongString();
	};	
}

/********************/

/**
	Creates the ToolInfo that identifies an Erlang timed transition,
	i.e., CONST::TransitionKind::Erlang 
*/
helper erlangTransitionToolInfo(rate : Real, k : Real) : PNML::ToolInfo {
	return object PNML::ToolInfo { 
		tool := CONST::ToolInfoConstants::toolName.toString();
		version := CONST::ToolInfoConstants::toolVersion.toString();
		toolInfoGrammarURI := CONST::TransitionKind::Exponential.toString().createURI();
		formattedXMLBuffer := erlang2String(rate,k);
	};	
}

helper erlang2String(rate : Real, k : Real) : LongString {
	return ("<value grammar=\"" + CONST::TransitionKind::Erlang.toString() + "\">" + rate.toString() + "</value>" + 
			"<value grammar=\"" + CONST::TransitionKind::Erlang.toString() + "\">" + k.toString() + "</value>").createLongString();
}

/**
	Creates the ToolInfo that identifies an deterministic timed transition,
	i.e., CONST::TransitionKind::Deterministic 
*/
helper determTransitionToolInfo(rate : Real) : PNML::ToolInfo {
	return object PNML::ToolInfo { 
		tool := CONST::ToolInfoConstants::toolName.toString();
		version := CONST::ToolInfoConstants::toolVersion.toString();
		toolInfoGrammarURI := CONST::TransitionKind::Exponential.toString().createURI();
		formattedXMLBuffer := ("<value grammar=\"" + CONST::TransitionKind::Deterministic.toString() + "\">" + rate.toString() + "</value>").createLongString();
	};	
}

/**
	Creates the ToolInfo that identifies an exponential timed transition,
	i.e., CONST::TransitionKind::Exponential 
*/
helper expTransitionToolInfo(rate : Real) : PNML::ToolInfo {
	return object PNML::ToolInfo { 
		tool := CONST::ToolInfoConstants::toolName.toString();
		version := CONST::ToolInfoConstants::toolVersion.toString();
		toolInfoGrammarURI := CONST::TransitionKind::Exponential.toString().createURI();
		formattedXMLBuffer := ("<value grammar=\"" + CONST::TransitionKind::Exponential.toString() + "\">" + rate.toString() + "</value>").createLongString();
	};	
}

/**
	Creates the ToolInfo that identifies an InfiniteServer timed transition,
	i.e., CONST::ServerType::InfiniteServer
*/
helper infServerTransitionToolInfo() : PNML::ToolInfo {
	return object PNML::ToolInfo {
		tool := CONST::ToolInfoConstants::toolName.toString();
		version := CONST::ToolInfoConstants::toolVersion.toString();
		toolInfoGrammarURI := CONST::ServerType::InfiniteServer.toString().createURI();
		formattedXMLBuffer := ("<value grammar=\"" + CONST::ServerType::InfiniteServer.toString() + "\"/>").createLongString();
	};
}

/**
	Creates the ToolInfo that identifies a OneServer timed transition,
	i.e., CONST::ServerType::OneServer 
*/
helper oneServerTransitionToolInfo() : PNML::ToolInfo {
	return object PNML::ToolInfo {
		tool := CONST::ToolInfoConstants::toolName.toString();
		version := CONST::ToolInfoConstants::toolVersion.toString();
		toolInfoGrammarURI := CONST::ServerType::OneServer.toString().createURI();
		formattedXMLBuffer := ("<value grammar=\"" + CONST::ServerType::OneServer.toString() + "\"/>").createLongString();
	};
}

/**
	Creates a ToolInfo that identifies a probabilistic immediate transition,
	i.e., CONST::TransitionKind::Immediate
*/
helper probTransitionToolInfo(prob: Real) : PNML::ToolInfo {
	return object PNML::ToolInfo {
		tool := CONST::ToolInfoConstants::toolName.toString();
		version := CONST::ToolInfoConstants::toolVersion.toString();
		toolInfoGrammarURI := CONST::TransitionKind::Immediate.toString().createURI();
		formattedXMLBuffer := ("<value grammar=\"" + CONST::TransitionKind::Immediate.toString() + "\">" + prob.toString() + "</value>").createLongString();
	};
}

/**
Creates the ToolInfo that creates priority information for a transition
i.e., CONST::TransitionKind::ImmediatePriority
*/
helper priorityTransitionToolInfo(priority : Integer) : PNML::ToolInfo{
return object PNML::ToolInfo { 
		tool := CONST::ToolInfoConstants::toolName.toString();
		version := CONST::ToolInfoConstants::toolVersion.toString();
		toolInfoGrammarURI := CONST::TransitionKind::ImmediatePriority.toString().createURI();
		formattedXMLBuffer := ("<value grammar=\"" + CONST::TransitionKind::ImmediatePriority.toString() + "\">" + priority.toString() + "</value>").createLongString();
	};
}

/**
	Creates a ToolInfo for the passed base time unit
	i.e., CONST::BaseUnitsConstants::baseTimeUnit
*/
helper baseTimeUnitToolInfo(unit : String) : PNML::ToolInfo {
	return object PNML::ToolInfo {
		tool := CONST::ToolInfoConstants::toolName.toString();
		version := CONST::ToolInfoConstants::toolVersion.toString();
		toolInfoGrammarURI := CONST::BaseUnitsConstants::baseTimeUnit.toString().createURI();
		formattedXMLBuffer := ("<value grammar=\"" + CONST::BaseUnitsConstants::baseTimeUnit.toString() + "\">" + unit  + "</value>").createLongString();
	};
}


/*******************************************************************************
  Tagged values utilities
*******************************************************************************/

/**
	Helper to get the key from a string in the form 'key=value'
*/
helper String::key() : String {
	assert fatal (self.indexOf("=") <> -1) with log ("Unexpected number of tokens in " + self);
	return self.substringBefore("=").trim()

}

/**
	Helper to get the value from a string in the form 'key=value'
*/
helper String::value() : String {
	assert fatal (self.indexOf("=") <> -1) with log ("Unexpected number of tokens in " + self);
	return self.substringAfter("=").trim()
}

/**
	Helper that determines if a given String represents a CallOperationExpression
*/
helper String::isExpression() : Boolean {
	/*
	distribution(mean:real,....)
	*/
	var trimmed := self.trim();
	return ((trimmed.indexOf("(") > 1) and 
			(trimmed.indexOf("(") < trimmed.size())) and 
			trimmed.trim().endsWith(")");
}

/**
	Helper that parses a CallOperationExpression and returns a Sequence
*/
helper String::asExpresion() : Sequence (String) {
	var lowercase := self.toLowerCase(); 
	var trimmed := lowercase.trim();
	assert warning ((trimmed.indexOf("(") > 1) and 
					(trimmed.indexOf("(") < trimmed.size()))
		   with log ("Tuple string '" + self + "' does not contain '('");
	assert warning (trimmed.trim().endsWith(")")) with log ("Tuple string '" + self + "' does not end with ')'");

	/**/
	var firstPar : Integer := trimmed.indexOf("(");
	var distribution : String := trimmed.substring(1, firstPar);
	/**/
	var segments : List (String) := List {};
	var pars : Integer := 0;
	var segment : String;
	segments->add(distribution);
	trimmed.substring(firstPar, trimmed.size() - 1).characters()->forEach(c) {
		switch {
			case (c = '(') {
				pars := pars + 1;
			} case (c = ')') {
				pars := pars - 1;
			} case (c = ',') {
				if (pars = 0) { 
					segments->add(segment);
					segment := '';
					continue;
				}
			}
		};
		segment := segment.concat(c);
	};
	segments->add(segment);
	var entries : Sequence (String) := Sequence {};
	segments->forEach(entry) {
		entries += entry;
	};
	return entries;
}

/**
	Helper that determines if a given String represents a Tuple
*/
helper String::isTuple() : Boolean {
	var trimmed := self.trim();
	return trimmed.startsWith("(") and trimmed.trim().endsWith(")");
}

/**
	Helper that parses a VSL Tuple and returns a Dictionary
*/
helper String::asTuple() : Dict (String, String) {
	var trimmed := self.trim();
	assert warning (trimmed.startsWith("(")) with log ("Tuple string '" + self + "' does not start with '('");
	assert warning (trimmed.trim().endsWith(")")) with log ("Tuple string '" + self + "' does not end with ')'");

	var segments : List (String) := List {};
	var pars : Integer := 0;
	var segment : String;
	trimmed.substring(2, trimmed.size() - 1).characters()->forEach(c) {
		switch {
			case (c = '(') {
				pars := pars + 1;
			} case (c = ')') {
				pars := pars - 1;
			} case (c = ',') {
				if (pars = 0) { 
					segments->add(segment);
					segment := '';
					continue;
				}
			}
		};
		segment := segment.concat(c);
	};
	segments->add(segment);
	var entries : Dict (String, String) := Dict {};
	segments->forEach(entry) {
		entries->put(entry.key(), entry.value());
	};
	return entries;
}
